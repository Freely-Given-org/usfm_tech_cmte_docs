start: i=id_line a=(para_simple+) finished() { make_usx(i, a) };

id_line: t=Tag('id') a=Word() Ws()+ b=(line?) { make_id(t, a, b) };

para_simple:   c=chapter Ws()* a=(para_content+) { do_error("Inserted paragraph after chapter", c, [c] + [make_para("p", a)]) }
             | chapter
             | t=Tag("tr") Ws()* a=(cell*) { make_tablerow(t, a) }
             | t=TagType("Paragraph") Ws()* a=(para_content*) { make_para(t, a) };

para_content: verse
             | note
             | milestone
             | break
             | char_simple;

cell: t=TagType('Cell') Ws()* a=(para_content*) { make_tablecell(t, a) };

chapter: c=chapter_def t=Tag("ca") Ws()* v=text e=EndTag()<CmpTag(t, e)> Ws()* { make_alt(t, v, c) }
        | chapter_def;

chapter_def: t=Tag("c") Ws()* a=Word() Ws()* { make_chapter(t, a) };

verse: v=verse_pub t=Tag("va") Ws()* c=text e=EndTag()<CmpTag(t, e)> { make_alt(t, c, v) }
        | verse_pub;

verse_pub: v=verse_def t=Tag("vp") Ws()* c=text e=EndTag()<CmpTag(t, e)> { make_pub(t, c, v) }
        | verse_def;

verse_def: t=Tag("v") Ws()* a=Word() Ws()* { make_verse(t, a) };

note: t=TagType("Note") Ws()* a=Word() Ws()* c=(char_simple*) e=EndTag()<CmpTag(t, e)> { make_note(t, a, c) };

milestone: figure
        | t=TagType("Milestone") Ws()* Pipe() a=(TokenType("Attribute")+) EndTag('') { make_milestone(t, a) }
        | t=TagType("Milestone") Ws()* EndTag('') { make_milestone(t, []) }
        | t=TagUnknown() { do_error("Unknown tag", t) };

figure: t=Tag("fig") Ws()* c=(prepipe?) Pipe() a=(TokenType("Attribute")+) EndTag("fig") { make_figattrib(t, c, a) }
        | t=Tag("fig") c=text e=EndTag()<CmpTag(t, e)> { make_fig(t, c) };

break: t=Tag("b") Ws()* { make_break() };

char_simple:   t=TagType("Character") Ws()* c=(prepipe?) Pipe() a=(TokenType("Attribute")+) e=EndTag()<CmpTag(t, e)>?  { make_char(t, c, a) }
             | t=TagType("Character") Ws()* c=(char_embed*) e=EndTag()<CmpTag(t, e)>?  { make_char(t, c) }
             | char_embed;

char_embed:  t=TagEmbed() Ws()* c=(prepipe?) Pipe() a=(TokenType("Attribute")+) e=EndTagEmbed()<CmpTag(t, e)>?  { make_char(t, c, a) }
             | t=TagEmbed() Ws()* c=(char_embed*) e=EndTagEmbed()<CmpTag(t, e)>?  { make_char(t, c) }
             | note
             | char_text;

char_text: a=(Word() | Ws())+ { str_join(a) };

text: a=Word() b=(Word() | Ws())* { str_join(a, b) };

line: a=Word() b=(Word() | c=Ws()<strneq(c, "\n")>)* d=Ws()<streq(d, "\n")> { str_join(a, b) };

prepipe: a=(TokenType("Word") | Ws())+ { str_join(a) };
